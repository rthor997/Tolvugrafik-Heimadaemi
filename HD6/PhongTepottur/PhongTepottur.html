<!DOCTYPE html>
<html>

<script id="vertex-shader" type="x-shader/x-vertex">

attribute vec4 vPosition;
attribute vec4 vNormal;
varying vec3 N, L, E;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform vec4 lightPosition;
uniform mat3 normalMatrix;

void main()
{
    vec3 pos = (modelViewMatrix * vPosition).xyz;

    // check for directional light
    if(lightPosition.w == 0.0) L = normalize(lightPosition.xyz);
    else L = normalize( lightPosition.xyz - pos );

    E =  -normalize(pos);
    N = normalize( normalMatrix*vNormal.xyz);

    gl_Position = projectionMatrix * modelViewMatrix * vPosition;

}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">

precision mediump float;

uniform vec4 ambientProduct;
uniform vec4 diffuseProduct;
uniform vec4 specularProduct;
uniform float shininess;
uniform bool useBlinnPhong;

varying vec3 N, L, E;

void main()
{
    vec3 NN = normalize(N);
    vec3 LL = normalize(L);
    vec3 EE = normalize(E);

    float Kd = max(dot(LL, NN), 0.0);
    vec4 diffuse = Kd * diffuseProduct;

    float Ks = 0.0;
    vec4 specular = vec4(0.0);

    if (useBlinnPhong) {
        // Half-vector
        vec3 H = normalize(LL + EE);
        Ks = pow(max(dot(NN, H), 0.0), shininess);
    } else {
        // Reflection vector
        vec3 R = reflect(-LL, NN);
        Ks = pow(max(dot(R, EE), 0.0), shininess);
    }

    if (dot(LL, NN) > 0.0) {
        specular = Ks * specularProduct;
    }

    vec4 ambient = ambientProduct;

    vec4 fColor = ambient + diffuse + specular;
    fColor.a = 1.0;

    gl_FragColor = fColor;
}   
</script>

<script type="text/javascript" src="../../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../../Common/initShaders.js"></script>
<script type="text/javascript" src="../../Common/MV.js"></script>
<script type="text/javascript" src="./teapotData.js"></script>
<script type="text/javascript" src="./PhongTepottur.js"></script>

<body>
    <button id="Breyta-Shading">Skipta um shading </button>
    <canvas id="gl-canvas" width="600" height="600"></canvas>
</body>
</html>
